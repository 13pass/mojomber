#!/usr/bin/perl

use FindBin;
use lib "$FindBin::Bin/mojo/lib";

use Mojolicious::Lite;
use Mojo::JSON;
use Mojo::ByteStream;
use Mojo::IOLoop;

my $ioloop = Mojo::IOLoop->singleton;

@ARGV = qw/daemon/ unless @ARGV;

my $players = {};

my $arena = [
    [1, 1, 1, 1, 1, 1, 1, 1, 1, 1],
    [1, 0, 0, 0, 0, 0, 0, 0, 0, 1],
    [1, 0, 0, 1, 0, 1, 1, 1, 0, 1],
    [1, 0, 1, 0, 0, 0, 0, 1, 0, 1],
    [1, 0, 0, 0, 0, 1, 0, 1, 0, 1],
    [1, 0, 1, 1, 0, 0, 0, 0, 0, 1],
    [1, 0, 0, 1, 0, 1, 1, 1, 0, 1],
    [1, 1, 0, 1, 0, 0, 0, 1, 0, 1],
    [1, 0, 0, 1, 0, 1, 0, 0, 0, 1],
    [1, 1, 1, 1, 1, 1, 1, 1, 1, 1]
];

websocket '/' => sub {
    my $self = shift;
    my $tx = $self->tx;

    app->log->debug('Player connected');

    my $cid = _get_id($self);
    $players->{$cid}->{tx} = $tx;

    my $player = $players->{$cid};

    $player->{nick}  = 'Bomber' . int(rand(10000));
    $player->{pos}   = _get_random_pos();
    $player->{frags} = 0;

    app->log->debug('Send arena');
    _send_message($self, type => 'drawarena', arena => $arena);

    app->log->debug('Send self info');
    _send_message($self, type => 'player', _get_player_info($cid));

    app->log->debug('Send other players info');
    my $other_players = [grep { $_->{id} ne $cid } @{_get_players()}];
    _send_message($self, type => 'initplayers', players => $other_players);

    app->log->debug('Notify other players about a new player');
    _send_message_to_other(
        $self,
        type => 'new_player',
        _get_player_info($cid)
    );

    $self->receive_message(
        sub {
            my ($self, $message) = @_;

            my $json = Mojo::JSON->new;

            $message = $json->decode($message);
            return unless $message || $json->error;

            my $type = $message->{type};

            if ($type eq 'move') {
                _handle_move($self, $message);
            }
            elsif ($type eq 'bomb') {
                _handle_bomb($self, $message);
            }
        }
    );

    $self->finished(
        sub {
            _send_message_to_other($self, type => 'old_player', id => $cid);

            app->log->debug('Player disconnected');
            delete $players->{$cid};
        }
    );
};

get '/' => 'index';

sub _handle_move {
    my $self = shift;
    my $message = shift;

    my $id = _get_id($self);
    my $direction = $message->{direction};

    return unless $id && $direction;

    my $player = $players->{$id};

    my $row = $player->{pos}->[0];
    my $col = $player->{pos}->[1];

    if ($direction eq 'up') {
        $row--;
    }
    elsif ($direction eq 'down') {
        $row++;
    }
    elsif ($direction eq 'left') {
        $col--;
    }
    elsif ($direction eq 'right') {
        $col++;
    }

    unless ($arena->[$row]->[$col]) {
        $player->{pos}->[0] = $row;
        $player->{pos}->[1] = $col;
        _send_message_to_other(
            $self,
            type      => 'move',
            id        => $id,
            direction => $direction
        );
    }
}

sub _get_id {
    my $self = shift;

    my $tx = $self->tx;
    return "$tx";
}

sub _handle_bomb {
    my $self = shift;
    my $message = shift;

    my $id = _get_id($self);

    my $player = $players->{$id};

    if (!$player->{bomb}) {
        app->log->debug('Player set up a bomb');

        $player->{bomb} = {pos => [@{$player->{pos}}]};

        _send_message_to_other(
            $self,
            type => 'bomb',
            id   => $id
        );

        $ioloop->timer(
            2,
            sub {
                my $bomb = delete $player->{bomb};

                my ($row, $col) = @{$bomb->{pos}};

                my @dead;
                my $me;
                _walk_arena(
                    $arena, $row, $col, 3,
                    sub {
                        my ($row, $col) = @_;

                        foreach my $pid (keys %$players) {
                            my ($p_row, $p_col) =
                              @{$players->{$pid}->{pos}};

                            if ($p_row eq $row && $p_col eq $col)
                            {
                                if ($id eq $pid) {
                                    $me = 1;
                                }
                                push @dead, $pid;
                            }
                        }
                    }
                );

                _send_message_to_all(
                    $self,
                    type => 'explode',
                    id   => $id
                );

                if (@dead) {
                    if ($me) {
                        $player->{frags} -= 1;
                    }
                    else {
                        $player->{frags} += @dead;
                    }

                    $ioloop->timer(
                        5,
                        sub {
                            my @players = map {
                                {id => $_, pos => _get_random_pos()}
                            } @dead;

                            foreach my $player (@players) {
                                $players->{$player->{id}}->{pos} =
                                  $player->{pos};
                            }

                            _send_message_to_all(
                                $self,
                                type    => 'alive',
                                id      => $id,
                                players => [ @players ]
                            );
                        }
                    );

                    _send_message_to_all(
                        $self,
                        type    => 'die',
                        players => [@dead]
                    );

                    _send_message_to_all(
                        $self,
                        type  => 'frags',
                        id    => $id,
                        frags => $player->{frags}
                    );
                }
            }
        );
    }
}

sub _walk_arena {
    my ($arena, $row, $col, $radius, $cb) = @_;

    my $mrow = @$arena;
    my $mcol = @{$arena->[0]};

    $cb->($row, $col);

    for (my $i = 1; $i < $radius; $i++) {
        if ($row + $i < $mrow) {
            last if $arena->[$row + $i]->[$col];

            $cb->($row + $i, $col);
        }
    }

    for (my $i = 1; $i < $radius; $i++) {
        if ($row - $i > 0) {
            last if $arena->[$row - $i]->[$col];

            $cb->($row - $i, $col);
        }
    }

    for (my $i = 1; $i < $radius; $i++) {
        if ($col + $i < $mcol) {
            last if $arena->[$row]->[$col + $i];

            $cb->($row, $col + $i);
        }
    }

    for (my $i = 1; $i < $radius; $i++) {
        if ($col - $i > 0) {
            last if $arena->[$row]->[$col - $i];

            $cb->($row, $col - $i);
        }
    }
}

sub _get_random_pos {
    my @pos;
    for (my $i = 0; $i < @$arena; $i++) {
        for (my $j = 0; $j < @{$arena->[0]}; $j++) {
            push @pos, [$i => $j] unless $arena->[$i]->[$j];
        }
    }

    my $rand = int(rand(@pos));

    return $pos[$rand];
}

sub _get_player_info {
    my $cid = shift;

    my $player = $players->{$cid};
    return unless $player;

    return (
        id    => $cid,
        pos   => $player->{pos},
        frags => $player->{frags},
        nick  => $player->{nick}
    );
}

sub _get_players {
    return [] unless keys %$players;

    return [map { { _get_player_info($_) } } keys %$players];
}

sub _message_to_json {
    my %message = @_;

    my $json = Mojo::JSON->new;
    return $json->encode({%message});
}

sub _send_message {
    my $self = shift;

    $self->send_message(_message_to_json(@_));
}

sub _send_message_to_other {
    my $self = shift;
    my %message = @_;

    my $id = _get_id($self);

    my $message = _message_to_json(%message);

    foreach my $cid (keys %$players) {
        next if $cid eq $id;

        if ($players->{$cid} && $players->{$cid}->{tx}) {
            $players->{$cid}->{tx}->send_message($message);
        }
        else {
            delete $players->{$cid};
        }
    }
}

sub _send_message_to_all {
    _send_message_to_other(@_);
    _send_message(@_);
}

print "Remember, you need to also run 'sudo perl mojo/examples/flash-policy-server.pl' as root for this to work...\n";

app->start;

1;

__DATA__

@@ index.html.ep
% my $url = $self->req->url->to_abs->scheme($self->req->is_secure ? 'wss' : 'ws')->path('/');
<!doctype html><html>
    <head>
        <title>Mojomber</title>
        <meta charset="utf-8" />
        <script type="text/javascript" src="/jquery.min.js"></script>
        <script type="text/javascript" src="/jquery.json.min.js"></script>
        <script type="text/javascript" src="/mojomber.js"></script>
        <script type="text/javascript">
            // Only load the flash fallback when needed
            if (!('WebSocket' in window)) {
                document.write([
                    '<scr'+'ipt type="text/javascript" src="/web-socket-js/swfobject.js"></scr'+'ipt>',
                    '<scr'+'ipt type="text/javascript" src="/web-socket-js/FABridge.js"></scr'+'ipt>',
                    '<scr'+'ipt type="text/javascript" src="/web-socket-js/web_socket.js"></scr'+'ipt>'
                ].join(''));
            }
        </script>
        <script type="text/javascript">
            if (WebSocket.__initialize) {
                // Set URL of your WebSocketMain.swf here:
                WebSocket.__swfLocation = '/web-socket-js/WebSocketMain.swf';
            }

            $(document).ready(function() {
                $('#content').mojomber({"url":"<%= $url %>"});
            });
        </script>
    </head>
    <body>
        <div class="container">
            <table border="0" height="100%" style="margin:auto">
            <tr>
            <td style="vertical-align:top"><div id="top"></div></td>
            <td style="vertical-align:middle">
                <div id="content"></div>
            </td></tr>
            </table>
        </div>
    </body>
</html>
